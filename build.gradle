apply plugin: 'java'

repositories {
  mavenCentral()
  google()
}
configurations {
  invoker
}

dependencies {
  implementation platform('com.google.cloud:libraries-bom:25.4.0')
  // Standard libraries needed
  implementation 'com.google.cloud.functions:functions-framework-api:1.0.4'
  implementation 'com.google.oauth-client:google-oauth-client-jetty:1.32.1'
  implementation 'com.google.apis:google-api-services-sheets:v4-rev20210629-1.32.1'
  implementation 'javax.mail:mail:1.5.0-b01'
  implementation 'com.google.cloud:google-cloud-secretmanager'

  // To run function locally using Functions Framework's local invoker
  invoker 'com.google.cloud.functions.invoker:java-function-invoker:1.1.0'

  // These dependencies are only used by the tests.
  testImplementation 'com.google.cloud.functions:functions-framework-api:1.0.4'
  testImplementation 'junit:junit:4.13.2'
  testImplementation 'com.google.truth:truth:1.1.3'
}

// Register a "runFunction" task to run the function locally
tasks.register("runFunction", JavaExec) {
  main = 'com.google.cloud.functions.invoker.runner.Invoker'
  classpath(configurations.invoker)
  inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
  args(
    '--target', project.findProperty('run.functionTarget') ?: '',
    '--port', project.findProperty('run.port') ?: 8080
  )
  doFirst {
    args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
  }
}